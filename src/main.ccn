start phase RootPhase {
    info = "Denotes the entry point of your compiler",

    actions {
        pass SPdoScanParse;
        Optimization;
        Print;
    }
};

traversal Print {
    uid = PRT
};

phase Optimization {
    actions {
        traversal OptSubstraction {
            uid = OS,
            nodes = { Binop }
        };
    }
};

/*** AST ***/

/**********************/
/* Enums **************/
/**********************/

enum MonOpType {
    prefix = MO,
    values {
        not, neg
    }
};

enum BinOpType {
    prefix = BO,
    values {
        add, sub, mul, div, mod, lt, le, gt, ge, eq, ne,
        and, or
    }
};

/**********************/
/* Nodesets ***********/
/**********************/

nodeset Expr = {NestedExpr, Binop, Monop, Cast, ProcCall, Var, ArrayExpr} | BasicType;
nodeset BasicType = {Num, Float, Bool};
nodeset RetType = BasicType | {Void};
nodeset Statement = {Assign, ProcCall, IfStatement, WhileLoop, DoWhileLoop, ForLoop, RetStatement, ArrayAssign};
nodeset Declaration = {FunDec, FunDef, GlobalDec, GlobalDef};
nodeset Block = {Statements} | Statement;
nodeset VarOptArrayVar = {Var, ArrayVar};
nodeset VarOptArrayExpr = {Var, ArrayExpr};

// Nodes you want to be able to link to from your symbol table.
nodeset Link = Expr | Statement;

/**********************/
/* Nodes **************/
/**********************/

root node Program {
    children {
        Declarations decls { constructor, mandatory }
    }
};

node Declarations {
    children {
        Declaration decl { constructor, mandatory },
        Declarations next { constructor }
    }
};

node FunDec {
    children {
        FunHeader funHeader { constructor, mandatory }
    },

    attributes {
        bool extern_att { constructor }            // has to be 'extern'
    }
};

node FunDef {
    children {
        FunHeader funHeader { constructor, mandatory },
        FunBody funBody { constructor, mandatory }
    },

    attributes {
        bool export { constructor }                       // if exists, has to be 'export'
    }
};

node GlobalDec {
    children {
        BasicType type { constructor, mandatory },
        VarOptArrayVar var { constructor, mandatory } 
    },

    attributes {
        bool extern_att { constructor }            // has to be 'extern'
    }
};

node GlobalDef {
    children {
        VarDec varDec { constructor, mandatory }
    },

    attributes {
        bool export { constructor }                       // if exists, has to be 'export'
    }
};

node FunHeader {
    children {
        RetType ret { constructor, mandatory },
        Var var { constructor, mandatory },
        Params params { constructor }
    }
};

node Params {
    children {
        Param param { constructor, mandatory },
        Params next { constructor }
    }
};

node Param {
    children {
        BasicType type { constructor, mandatory },
        VarOptArrayVar var { constructor, mandatory }
    }
};

node FunBody {
    children {
        VarDecs varDecs { constructor },
        LocalFunDefs localFunDefs { constructor },      // Extension 1
        Statements stmts { constructor }
    }
};

node VarDecs {
    children {
        VarDec varDec { constructor },
        VarDecs next { constructor }
    }
};

node LocalFunDefs {
    children {
        LocalFunDef localFunDef { constructor },
        LocalFunDefs next { constructor }
    }
};

// Extension 1
node LocalFunDef {
    children {
        FunHeader funHeader { constructor, mandatory },
        FunBody funBody { constructor, mandatory }
    }
};

node Statements {
    children {
        Statement stmt { constructor },
        Statements next { constructor }
    }
};

node Assign {                                   // Id = Expr
    children {
        Var var { constructor },
        Expr expr { constructor, mandatory }
    }
};

node Binop {
    children {
        Expr left { constructor, mandatory },
        Expr right { constructor, mandatory }
    },

    attributes {
        BinOpType op { constructor }
    }
};

node Monop {
    children {
        Expr left { constructor, mandatory }
    },

    attributes {
        MonOpType op { constructor }
    }
};

node VarDec {
    children {
        BasicType type { constructor, mandatory },
        VarOptArrayExpr var { constructor, mandatory },
        ArrInit expr { constructor }
    }
};

node ProcCall {
    children {
        Var let { constructor, mandatory },
        Exprs exprs { constructor }
    }
};

node Exprs {
    children {
        Expr expr { constructor, mandatory },
        Exprs next { constructor }
    }
};

node IfStatement {
    children {
        Expr expr { constructor, mandatory },
        Block block { constructor, mandatory },
        ElseStatement else_block { constructor }
    }
};

node ElseStatement {
    children {
        Block block { constructor, mandatory }
    }
};

node WhileLoop {
    children {
        Expr expr { constructor, mandatory },
        Block block { constructor, mandatory }
    }
};

node DoWhileLoop {
    children {
        Block block { constructor, mandatory },
        Expr expr { constructor, mandatory }
    }
};

node ForLoop {
    children {
        Assign assign { constructor, mandatory },
        Expr cond { constructor, mandatory },
        Expr iter { constructor },
        Block block { constructor, mandatory }
    }
};

node RetStatement {
    children {
        Expr expr { constructor }
    }
};

// Expr -> ( Expr )
node NestedExpr {
    children { 
        Expr expr { constructor, mandatory }
    }
};

// ( BasicType ) Expr
node Cast {
    children {
        BasicType type { constructor, mandatory },
        Expr expr { constructor, mandatory }
    }
};

node Void {
};

node Var {
    attributes {
        string name { constructor, mandatory },
        Link decl
    }
};

node ArrayVar {
    children {
        DimensionVars dims { constructor, mandatory },
        Var var { constructor, mandatory }
    }
};

node DimensionVars {
    children {
        Var dim { constructor, mandatory },
        DimensionExprs next { constructor }
    }
};

node ArrayExpr {
    children {
        DimensionExprs dims { constructor, mandatory },
        Var var { constructor, mandatory }
    }
};

node DimensionExprs {
    children {
        Expr dim { constructor, mandatory },
        DimensionExprs next { constructor }
    }
};

node ArrInit {
    children {
        Expr expr { constructor, mandatory },
        ArrInit next { constructor }
    }
};

node ArrayAssign {
    children {
        ArrayExpr var { constructor, mandatory },
        Expr expr { constructor, mandatory }
    }
};

node Num {
    attributes {
        int val { constructor }
    }
};

node Float {
    attributes {
        float val { constructor }
    }
};

node Bool {
    attributes {
        bool val { constructor }
    }
};
