start phase RootPhase {
    info = "Denotes the entry point of your compiler",

    actions {
        pass SPdoScanParse;
        Optimization;
        Print;
    }
};

traversal Print {
    uid = PRT
};

phase Optimization {
    actions {
        traversal OptSubstraction {
            uid = OS,
            nodes = { Binop }
        };
    }
};

/*** AST ***/

/**********************/
/* Enums **************/
/**********************/

enum MonOpType {
    prefix = MO,
    values {
        not, neg
    }
};

enum BinOpType {
    prefix = BO,
    values {
        add, sub, mul, div, mod, lt, le, gt, ge, eq, ne,
        and, or
    }
};

/**********************/
/* Nodesets ***********/
/**********************/

nodeset Expr = {NestedExpr, Binop, Monop, Cast, ProcCall, Var} | BasicType;
nodeset BasicType = {Num, Float, Bool};
nodeset RetType = BasicType | {void};
nodeset Stmt = {Assign, ProcCall, IfStmt, WhileLoop, DoWhileLoop, ForLoop, RetStmt};
nodeset Declaration = {FunDec, FunDef, GlobalDec, GlobalDef};
nodeset Block = {Stmts} | stmt;
nodeset Vars {
    nodes = {VarLet, Var},

    attributes {
        string name,
        Link decl
    }
};

// Nodes you want to be able to link to from your symbol table.
nodeset Link = Expr | Stmt;

/**********************/
/* Nodes **************/
/**********************/

root node Program {
    children {
        Declarations decls { constructor, mandatory },
        Declaration decl { constructor, mandatory }
    }
};

node Declarations {
    children {
        Declaration decl { constructor, mandatory },
        Declarations next { constructor }
    }
};

node FunDec {
    children {
        FunHeader funHeader { constructor, mandatory }
    },

    attributes {
        string extern { constructor, mandatory }            // has to be 'extern'
    }
};

node FunDef {
    children {
        FunHeader funHeader { constructor, mandatory },
        FunBody funBody { constructor, mandatory }
    },

    attributes {
        string export { constructor }                       // if exists, has to be 'export'
    }
};

node GlobalDec {
    children {
        BasicType type { constructor, mandatory },
        Var var { constructor, mandatory } 
    },

    attributes {
        string extern { constructor, mandatory }            // has to be 'extern'
    }
};

node GlobalDef {
    children {
        BasicType type { constructor, mandatory },
        VarDec varDec { constructor, mandatory }
    },

    attributes {
        string export { constructor }                       // if exists, has to be 'export'
    }
};

node FunHeader {
    children {
        RetType ret { constructor, mandatory },
        Var var { constructor, mandatory },
        Params params { constructor }
    }
};

node Params {
    children {
        Param param { constructor, mandatory },
        Params next { constructor }
    }
};

node Param {
    children {
        BasicType type { constructor, mandatory },
        Var var { constructor, mandatory }
    }
};

node FunBody {
    children {
        VarDecs varDecs { constructor },
        LocalFunDefs localFunDefs { constructor },      // Extension 1
        Stmts stmts { constructor }
    }
};

node VarDecs {
    children {
        VarDec varDec { constructor },
        VarDecs next { constructor }
    }
};

node LocalFunDefs {
    children {
        LocalFunDef localFunDef { constructor },
        LocalFunDefs next { constructor }
    }
};

// Extension 1
node LocalFunDef {
    children {
        FunHeader funHeader { constructor, mandatory },
        FunBody funBody { constructor, mandatory }
    }
};

node Stmts {
    children {
        Stmt stmt { constructor },
        Stmts next { constructor }
    }
};

node Assign {                                   // Id = Expr
    children {
        VarLet let { constructor },
        Expr expr { constructor, mandatory }
    }
};

node Binop {
    children {
        Expr left { constructor, mandatory },
        Expr right { constructor, mandatory }
    },

    attributes {
        BinOpType op { constructor }
    }
};

node Monop {
    children {
        Expr left { constructor, mandatory }
    },

    attributes {
        MonOpType op { constructor }
    }
};

node VarDec {
    children {
        BasicType type { constructor, mandatory },
        Var var { constructor, mandatory },
        Expr expr { constructor }
    }
};

node ProcCall {
    children {
        VarLet let { constructor, mandatory },
        Exprs exprs { constructor }
    }
};

node Exprs {
    children {
        Expr expr { constructor, mandatory },
        Exprs next { constructor }
    }
};

node IfStmt {
    children {
        Expr expr { constructor, mandatory },
        Block block { constructor, mandatory },
        ElseStmt else { constructor }
    }
};

node ElseStmt {
    children {
        Block block { constructor, mandatory }
    }
};

node WhileLoop {
    children {
        Expr expr { constructor, mandatory },
        Block block { constructor, mandatory }
    }
};

node DoWhileLoop {
    children {
        Block block { constructor, mandatory },
        Expr expr { constructor, mandatory }
    }
};

node ForLoop {
    children {
        Assign assign { constructor, mandatory },
        Expr cond { constructor, mandatory },
        Expr iter { constructor },
        Block block { constructor, mandatory }
    }
};

node RetStmt {
    children {
        Expr expr { constructor }
    }
};

// Expr -> ( Expr )
node NestedExpr {
    children { 
        Expr expr { constructor, mandatory }
    }
};

// ( BasicType ) Expr
node Cast {
    children {
        BasicType type { constructor, mandatory },
        Expr expr { constructor, mandatory }
    }
};

node void {
};

node VarLet {
    attributes {
        string name { constructor }  // Repeat from nodeset to add constructor
    }
};

node Var {
    attributes {
        string name { constructor }
    }
};

node Num {
    attributes {
        int val { constructor }
    }
};

node Float {
    attributes {
        float val { constructor }
    }
};

node Bool {
    attributes {
        bool val { constructor }
    }
};
